buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.1.1"
group = "tv.dotstart.mc"
archivesBaseName = "icbm"

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.10.2-12.18.2.2122"
    runDir = "run"
    mappings = "snapshot_20160518"
}

sourceSets {
    api
}

jar {
    from sourceSets.api.output
    dependsOn apiClasses

    // specify which files are really included, can control which APIs should be in
    include "tv/dotstart/mc/icbm/**"
    include "assets/**"
    include "mcmod.info"
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

task javadocs(type: Javadoc) {
    source = sourceSets.api.java
    include "tv/dotstart/mc/icbm/api/**"

    classpath = configurations.compile + configurations.forgeGradleMc + configurations.forgeGradleMcDeps
}

task utilJar(type: Jar) {
    from(sourceSets.api.output)
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task apiJar(type: Jar) {
    from sourceSets.api.java
    include "tv/dotstart/mc/icbm/api/**"

    from sourceSets.api.output
    include "tv/dotstart/mc/icbm/api/**"

    classifier = 'api'
}

artifacts {
    archives apiJar
    archives javadocJar
    archives sourceJar
}

dependencies {
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
